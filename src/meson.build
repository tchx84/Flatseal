pkgdatadir = join_paths(get_option('datadir'), meson.project_name())
gnome = import('gnome')

dependency('appstream', version : '>= 1.0')
dependency('libadwaita-1', version : '>= 1.5')
dependency('webkitgtk-6.0', version : '>= 2.40')

src_res = gnome.compile_resources('com.github.tchx84.Flatseal.src',
  'com.github.tchx84.Flatseal.src.gresource.xml',
  gresource_bundle: true,
  install: true,
  install_dir: pkgdatadir,
)

data_res = gnome.compile_resources('com.github.tchx84.Flatseal.data',
  'com.github.tchx84.Flatseal.data.gresource.xml',
  gresource_bundle: true,
  install: true,
  install_dir: pkgdatadir,
)

bin_conf = configuration_data()
bin_conf.set('GJS', find_program('gjs').full_path())
bin_conf.set('PACKAGE_VERSION', meson.project_version())
bin_conf.set('PACKAGE_NAME', meson.project_name())
bin_conf.set('prefix', get_option('prefix'))
bin_conf.set('libdir', join_paths(get_option('prefix'), get_option('libdir')))
bin_conf.set('datadir', join_paths(get_option('prefix'), get_option('datadir')))

configure_file(
  input: 'com.github.tchx84.Flatseal.in',
  output: 'com.github.tchx84.Flatseal',
  configuration: bin_conf,
  install: true,
  install_dir: get_option('bindir')
)
